#include <TinyGPS++.h>
#include <HardwareSerial.h>
#include <Wire.h>
#include <Adafruit_Sensor.h>
#include <Adafruit_BMP3XX.h>
#include <Adafruit_MPU6050.h>

// GPS Module setup
const int rxPin = 16;
const int txPin = 17;
const uint32_t GPSBaud = 9600;
TinyGPSPlus gps;
HardwareSerial GPS_Serial(2);

// Barometer (BMP388) setup
#define SEALEVELPRESSURE_HPA (1013.25)
Adafruit_BMP3XX bmp;

// Gyroscope (MPU6050) setup
Adafruit_MPU6050 mpu;

void setup() {
  // Start serial communication for all sensors
  Serial.begin(115200);
  
  // GPS module initialization
  GPS_Serial.begin(9600, SERIAL_8N1, rxPin, txPin);
  
  // Barometer (BMP388) initialization
  if (!bmp.begin_I2C()) {
    Serial.println("BMP388 not found.");
    while (1);
  }
  bmp.setTemperatureOversampling(BMP3_OVERSAMPLING_8X);
  bmp.setPressureOversampling(BMP3_NO_OVERSAMPLING);
  bmp.setIIRFilterCoeff(BMP3_IIR_FILTER_COEFF_3);
  bmp.setOutputDataRate(BMP3_ODR_50_HZ);
  
  // Gyroscope (MPU6050) initialization
  if (!mpu.begin()) {
    Serial.println("Failed to find MPU6050.");
    while (1);
  }
  mpu.setAccelerometerRange(MPU6050_RANGE_8_G);
  mpu.setGyroRange(MPU6050_RANGE_500_DEG);
  mpu.setFilterBandwidth(MPU6050_BAND_21_HZ);
}

void loop() {
  // ----- GPS Data -----
  while (GPS_Serial.available() > 0) {
    gps.encode(GPS_Serial.read());
    if (gps.location.isUpdated()) {
      Serial.print("Latitude: ");
      Serial.println(gps.location.lat(), 6);
      Serial.print("Longitude: ");
      Serial.println(gps.location.lng(), 6);
      Serial.print("Altitude (feet): ");
      Serial.println(gps.altitude.feet());
      Serial.print("Speed (mph): ");
      Serial.println(gps.speed.mph());
      Serial.println();
    }
  }

  // ----- Barometer (BMP388) Data -----
  if (!bmp.performReading()) {
    Serial.println("Failed to perform barometer reading.");
    return;
  }
  Serial.print("Pressure: ");
  Serial.print(bmp.pressure / 100.0);
  Serial.println(" hPa");
  
  Serial.print("Altitude: ");
  Serial.print(bmp.readAltitude(SEALEVELPRESSURE_HPA) * 3.28084);
  Serial.println(" ft");
  Serial.println();
  
  // ----- Gyroscope (MPU6050) Data -----
  sensors_event_t a, g, temp;
  mpu.getEvent(&a, &g, &temp);
  
  Serial.print("Acceleration = X: ");
  Serial.print(a.acceleration.x);
  Serial.print(", Y: ");
  Serial.print(a.acceleration.y);
  Serial.print(", Z: ");
  Serial.print(a.acceleration.z);
  Serial.println(" m/s^2");

  Serial.print("Rotation = X: ");
  Serial.print(g.gyro.x);
  Serial.print(", Y: ");
  Serial.print(g.gyro.y);
  Serial.print(", Z: ");
  Serial.print(g.gyro.z);
  Serial.println(" rad/s");

  Serial.print("Temperature: ");
  Serial.print((temp.temperature) * (9.0/5.0) + 32.0);
  Serial.println(" *F");

  Serial.println();
  delay(2000);  // Delay between data refreshes
}
